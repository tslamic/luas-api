plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'com.diffplug.gradle.spotless' version '3.13.0'
    id 'com.gradle.build-scan' version '1.15.1'
    id 'me.tadej.versioning' version '0.1.1'
}

apply plugin: 'jacoco'

group = 'me.tadej'
version = versioning.versionName()

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    testCompile 'com.google.truth:truth:0.42'
}

spotless {
    kotlin {
        target '**/*.kt'
        ktlint('0.24.0')
        licenseHeaderFile project.rootProject.file('misc/header.txt')
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def CI = System.getenv('CI') == 'true'

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    if (CI) {
        publishAlways()
        tag 'CI'
    }
}
